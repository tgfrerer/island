message(STATUS "Build type: ${CMAKE_BUILD_TYPE}")

# Hot-reloading is enabled by default for debug target, 
# whilst release target are built as statically linked 
# binaries by default. 
# This can be changed on a project-by-project basis, as
# we only set initial default settings here.

if( CMAKE_BUILD_TYPE STREQUAL "Debug" )
    # we enable hot-reloading by default for debug target
    set(PLUGINS_DYNAMIC ON CACHE BOOL "Use dynamic linking for all plugins")
else()
    # we disdable hot-reloading by default for release target
    set(PLUGINS_DYNAMIC OFF CACHE BOOL "Use dynamic linking for all plugins")
endif()

set ( STATIC_LIBS )

set ( PLUGIN_LIBS_DEPENDENCIES "")
set ( PLUGIN_LINK_DIRS "")

# specify output directory
set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/modules)
set(CMAKE_MODULE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/modules)
set(CMAKE_LIBRARY_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/modules)
set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR})

# get git commit version for island framework
execute_process(COMMAND
  git describe --match=NeVeRmAtCh --always --abbrev=40 --dirty
  WORKING_DIRECTORY "${ISLAND_BASE_DIR}"
  OUTPUT_VARIABLE ISLAND_GIT_SHA1
  ERROR_QUIET OUTPUT_STRIP_TRAILING_WHITESPACE)


function (link_resources param_target param_link_name)
    # create a link to local resources
    if (WIN32)
        if (EXISTS "${param_link_name}")
            message(STATUS "local resources exists ${param_link_name}")
        else()    
            get_filename_component(real_path "${param_link_name}" REALPATH)
            string(REPLACE "/" "\\" target_path "${real_path}")

            get_filename_component(real_path "${param_target}" REALPATH)
            string(REPLACE "/" "\\" src_path "${real_path}")

            execute_process(COMMAND cmd /C mklink /J "${target_path}" "${src_path}" 
                RESULT_VARIABLE link_result 
                OUTPUT_VARIABLE link_output)
            message(STATUS "created symbolic link ${target_path} ${src_path} ${link_output} ${link_result}")
        endif()    
        
    else()
        if (EXISTS "${param_link_name}")
            message(STATUS "local resources exists ${param_link_name}")
        else()
            execute_process(COMMAND ln -sfn ${param_target} ${param_link_name} )
            message(STATUS "created symbolic link ${param_target} ${param_link_name}")
        endif()
    endif()

endfunction(link_resources)

if (REQUIRES_ISLAND_CORE)

    # add glm include directory to includes search path 
    include_directories(${ISLAND_BASE_DIR}/3rdparty/src/glm/)

    # create a link to shared resources
    link_resources(${ISLAND_BASE_DIR}/resources ${CMAKE_BINARY_DIR}/resources )
    
    #  DEPRECATED: Uncomment this if you wish not to use the ubuntu package `lunarg-vulkan-sdk`.
    ## Check if a local copy of the Vulkan SDK exists, and the environment VULKAN_SDK points to it.
    ## If yes, use this directory for header includes, and linking
    ## we need this on windows

    if (WIN32)
        set (VULKAN_SDK_ENV $ENV{VULKAN_SDK})
        if (EXISTS ${VULKAN_SDK_ENV})
    
        #include vulkan from SDK installation directory
        include_directories(${VULKAN_SDK_ENV}/include/)
    
        # NOTE: We're linking Vulkan from this system's Vulkan SDK directory
        link_directories(${VULKAN_SDK_ENV}/lib)
    
        endif(EXISTS ${VULKAN_SDK_ENV})
    endif()
endif()

# Adds a directory to the modules locations
macro(add_island_module_location MODULE_LOCATION)
    set( MODULE_LOCATIONS_LIST ${MODULE_LOCATIONS_LIST} ${MODULE_LOCATION} CACHE INTERNAL "module_locations_list" )
    include_directories("${MODULE_LOCATIONS_LIST}")
endmacro()


# include the island base directory
include_directories ("${ISLAND_BASE_DIR}")

# We will store all possible module locations in this list, if the same module exists in multiple locations, the last one will be used
set ( MODULE_LOCATIONS_LIST CACHE INTERNAL "module_locations_list")

# Set default island module location
add_island_module_location("${ISLAND_BASE_DIR}/modules")

# We will store all requested module names in this list, and then load modules based on this list
set ( MODULES_LIST )

# We will store all loaded module names in this global list, so that we can make sure that modules don't get loaded more than once.
set ( LOADED_MODULES_LIST CACHE INTERNAL "loaded_modules_list" )

# These modules are always loaded - they control the plugin system.
set ( ISLAND_LOADER_MODULES le_file_watcher;le_core )

# These modules from the renderer - if you want to draw graphics, you want these.
# Note that order matters: dependent modules must be named before their dependencies.
set ( CORE_ISLAND_MODULES le_pipeline_builder;le_window;le_backend_vk;le_swapchain_vk;le_renderer;le_jobs;le_shader_compiler)

# Add required modules to modules list based on user flags
#
if (REQUIRES_ISLAND_LOADER)
    list (APPEND MODULES_LIST ${ISLAND_LOADER_MODULES})
endif()
 
if (REQUIRES_ISLAND_CORE)
   list (APPEND MODULES_LIST ${CORE_ISLAND_MODULES})
endif()

# Loads a requested module
# Note that this method is only called in epilog - this is a mechanism to make sure 
# we have a consolidated, unique list of modules by the time we load modules.
macro(load_island_module MODULE_NAME)

    # look for the module in all given locations
    set( MODULE_LOCATION )
    foreach( LOCATION ${MODULE_LOCATIONS_LIST} )
        set( CURRENT_LOCATION "${LOCATION}/${MODULE_NAME}" )
        if( EXISTS ${CURRENT_LOCATION} )
            set( MODULE_LOCATION ${CURRENT_LOCATION} )
        endif()
    endforeach()

    if( MODULE_LOCATION )
        message(STATUS "Loading module  : ${MODULE_NAME}")
        add_subdirectory (${MODULE_LOCATION} ${MODULE_NAME})
    else()
        message( SEND_ERROR "Module not found  : ${MODULE_NAME}")
    endif()
endmacro()

# Call this macro from other modules to establish a dependency.
# Adds a module name to list of requested modules, 
# checks whether a module was already requested to prevent duplicates.
macro(add_island_module MODULE_NAME)
    
    if (NOT ${MODULE_NAME} IN_LIST LOADED_MODULES_LIST)
        # prepend module name to loaded_modules_list in global scope
        set( LOADED_MODULES_LIST ${LOADED_MODULES_LIST} ${MODULE_NAME} CACHE INTERNAL "loaded_modules_list" )        
        load_island_module(${MODULE_NAME})        
    else()
        # message(STATUS "[ NOTE ] Rejecting add_island_module request: `${MODULE_NAME}` - Module already present."  )
    endif()

endmacro(add_island_module)

macro(add_static_lib LIB_NAME)

    if (NOT ${LIB_NAME} IN_LIST STATIC_LIBS)
        set (STATIC_LIBS ${STATIC_LIBS} ${LIB_NAME} PARENT_SCOPE)
    else()
        set (STATIC_LIBS ${STATIC_LIBS} PARENT_SCOPE)
        # message(STATUS "[ NOTE ] Rejecting extra static lib addition: `${LIB_NAME}` - lib already present."  )
    endif()

endmacro(add_static_lib LIB_NAME)

macro (depends_on_island_module MODULE_NAME)
    set (STORED_STATIC_LIBS ${STATIC_LIBS} )
    set (STATIC_LIBS ${STATIC_LIBS} ${TARGET})

    add_island_module(${MODULE_NAME})
    
    # message(STATUS "*** Current static libs: ${STATIC_LIBS}")
    set (STATIC_LIBS ${STATIC_LIBS})

endmacro(depends_on_island_module)

macro(add_dynamic_linker_flags)
    
    if (CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
        
        # IMPORTANT: --no-gnu-unique for gcc compiler
        #
        # This compiler flag is neccesary as otherwise the library may get compiled
        # with some symbols exported as UNIQUE, which implicitly makes this library
        # un-closeable. This means, any calls to `dlclose` on this library, once
        # loaded, will have no effect, and autoreload for this library will not work
        # as the first version of the library will remain resident.
        
        target_compile_options (${TARGET} PUBLIC --no-gnu-unique)
        
    endif()

    if (WIN32 AND NOT ${TARGET} MATCHES "le_core")

        # On windows, when hot-reloading, we must make sure we can actually write 
        # our artifacts even if the program is running. To be sure, we attempt to
        # first move any old artifacts which may still be in use by renaming them.
        # 
        set(TARGET_ARTIFACT "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${TARGET}")
        
        add_custom_command(TARGET ${TARGET} 
                           PRE_LINK
                           COMMAND ${CMAKE_COMMAND}
                               -DFileToCheck=${TARGET_ARTIFACT}.dll
                               -DMethod=Rename
                               -P ${ISLAND_BASE_DIR}/CMakeLists.txt.rename_artifact.in
                           COMMENT "Checking if ${TARGET_ARTIFACT}.dll exists...")

        add_custom_command(TARGET ${TARGET} 
                           PRE_LINK
                           COMMAND ${CMAKE_COMMAND}
                               -DFileToCheck=${TARGET_ARTIFACT}.pdb
                               -DMethod=Rename
                               -P ${ISLAND_BASE_DIR}/CMakeLists.txt.rename_artifact.in
                           COMMENT "Checking if ${TARGET_ARTIFACT}.pdb exists...")

        # To signal to the hot-reloading file watcher that a re-compile has occured,
        # we touch a flag file which shares the same basename as the artifact, and 
        # has the file ending ".flag" - We want to update this only at the very end 
        # of the build process to make sure that all file writing operations have 
        # completed - if we watch the .dll directly there is a chance that the hot-
        # reloading mechanism gets triggered mutliple times because the .dll may get
        # accessed a couple of times during the build process.
        # 
        # by using a flag file which we touch only once at the end of the build we
        # whould have a fairly atomic marker by which to tell whether the build 
        # process has truly completed.
        add_custom_command(TARGET ${TARGET} 
                           POST_BUILD
                           COMMAND ${CMAKE_COMMAND}
                               -DFileToCheck=${TARGET_ARTIFACT}.flag
                               -DMethod=Touch
                               -P ${ISLAND_BASE_DIR}/CMakeLists.txt.rename_artifact.in
                           COMMENT "Touchting ${TARGET_ARTIFACT}.flag")

        # additionally, we don't want incremental linking, because that will 
        # fail anyway, for it won't be able to find the last version of the .dll, 
        # since we've just removed it.
        set_target_properties(${TARGET} PROPERTIES LINK_FLAGS "/INCREMENTAL:NO")

        # on windows we must link all libaries with le_core so that they can find 
        # le_core's exported functions which are used for loading module apis.

        target_link_libraries(${TARGET} PRIVATE "le_core" )

    endif()

    
endmacro(add_dynamic_linker_flags)
